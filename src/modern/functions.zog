fn init() {
  scoreboard objectives add harl.modern.life.is.rubbish dummy
  scoreboard objectives add harl.modern dummy
  execute in harl:void run function harl:modern/place_island
}

fn place_island() {
  forceload add -1475 -1420 -1428 -1373
  place template harl:modern/island -1475 0 -1420

  gamerule commandModificationBlockLimit 1000000
  fillbiome -1475 0 -1420 -1428 45 -1373 cherry_grove
  gamerule commandModificationBlockLimit 32768

  forceload remove -1475 -1420 -1428 -1373
}

fn dialog1() {
  data modify storage harl:dialog lines set value ['{"text":"Wings?","color":"dark_green"}','{"text":"Yes, wings. To reach the sky. To travel. To dream.","color":"dark_aqua"}','{"text":"And did they fly?","color":"dark_green"}','{"text":"I think they did. Did you not see it? They reached the top and flew. They saw the wide world and dreamt of what could be.","color":"dark_aqua"}','{"text":"Did they stay there?","color":"dark_green"}','{"text":"No. They came back to earth carrying their dreams, but their head was still stuck in the clouds.","color":"dark_aqua"}']
  schedule function harl:dialog/speak 1s
}

fn dialog2() {
  data modify storage harl:dialog lines set value ['{"text":"Why did they come down?","color":"dark_green"}','{"text":"To find themselves. They saw the vastness of the world and thought to dig a little deeper to find themselves.","color":"dark_aqua"}','{"text":"And what did they find?","color":"dark_green"}','{"text":"That life didn\'t always make sense and that somethings were sweet - bitter-sweet, but even if everything was upside down and didn\'t make sense that there was always something special to find.","color":"dark_aqua"}']
  schedule function harl:dialog/speak 1s
}

fn remove_island1() {
  kill @e[type=villager,tag=harl.modern.villager,distance=0..]
  kill @e[type=bee,distance=0..]
  forceload add -1475 -1420 -1450 -1373

  gamerule commandModificationBlockLimit 1000000
  gamerule doTileDrops false
  fill -1475 0 -1420 -1450 32 -1373 air
  stopsound @a block
  gamerule doTileDrops true
  gamerule commandModificationBlockLimit 32768

  forceload remove -1475 -1420 -1450 -1373
}

fn remove_island2() {
  forceload add -1449 -1420 -1428 -1373

  gamerule commandModificationBlockLimit 1000000
  gamerule doTileDrops false
  fill -1449 0 -1420 -1428 45 -1373 air
  stopsound @a block
  gamerule doTileDrops true
  gamerule commandModificationBlockLimit 32768

  place template harl:modern/tunnel -1448 9 -1420
  forceload remove -1449 -1420 -1428 -1373
}

fn exit() {
  tag @s remove harl.modern.player
  schedule clear harl:modern/decay/iterate
  kill @e[type=block_display,tag=harl.modern.decay]

  remove/classic()
}

module remove {
  fn classic() {
    execute in harl:void run function harl:classic/remove_island
    schedule function harl:modern/remove/beta 4t
  }

  fn beta() {
    execute in harl:void run function harl:beta/remove_island
    schedule function harl:modern/remove/release 4t
  }

  fn release() {
    execute in harl:void run function harl:release/remove_island
    schedule function harl:modern/remove/modern1 4t
  }

  fn modern1() {
    execute in harl:void run function harl:modern/remove_island1
    schedule function harl:modern/remove/modern2 4t
  }

  fn modern2() {
    execute in harl:void run function harl:modern/remove_island2
    schedule function harl:city/init 4t
  }
}

fn _tick() {
  execute in harl:void positioned -1475 0 -1420 as @a[tag=!harl.modern.player,dx=48,dy=48,dz=48,nbt={OnGround:1b}] run function harl:modern/enter

  execute unless entity @a[tag=harl.modern.player] run return -1
  data merge entity @e[type=villager,tag=harl.modern.villager,name=Dinnerbone,limit=1] {VillagerData:{profession:"minecraft:farmer"},Offers:{Recipes:[{rewardExp:0b,maxUses:1,buy:{id:"minecraft:pumpkin_seeds",count:4},sell:{id:"minecraft:glass_bottle",count:1}}]}}

  execute as @e[type=block_display,tag=harl.modern.decay,scores={harl.modern.life.is.rubbish=60}] run data modify entity @s teleport_duration set value 60
  execute as @e[type=block_display,tag=harl.modern.decay,scores={harl.modern.life.is.rubbish=59}] at @s run function harl:modern/decay/begin_animation
  scoreboard players remove @e[type=block_display,tag=harl.modern.decay] harl.modern.life.is.rubbish 1
  kill @e[type=block_display,tag=harl.modern.decay,scores={harl.modern.life.is.rubbish=0}]

  execute in harl:void positioned -1431 10 -1405 as @a[tag=harl.modern.player,distance=..2] run function harl:modern/exit
}

fn enter() {
  :release/exit()
  tag @s add harl.modern.player
  spawnpoint @s -1445 29 -1403
  dialog1()
}

module decay {
  fn init() {
    advancement revoke @s only harl:modern/drink_honey
    dialog2()
    iterate()
  }

  fn stop() {
    schedule clear harl:modern/decay/iterate
  }

  fn iterate() {
    execute in harl:void positioned -1451 0 -1396 summon block_display run function harl:modern/decay/init_display
    schedule function harl:modern/decay/iterate 1t replace
  }

  fn init_display() {
    data modify entity @s interpolation_duration set value 60
    tag @s add harl.modern.decay
    scoreboard players set @s harl.modern.life.is.rubbish 61

    spreadplayers ~ ~ 1 20 true @s
    execute at @s align xyz run tp @s ~ ~-1 ~

    execute at @s if block ~ ~ ~ bedrock run kill @s
    execute unless entity @s run return -1
    
    execute at @s run loot spawn ~ ~ ~ mine ~ ~ ~ minecraft:netherite_pickaxe[minecraft:enchantments={levels:{"minecraft:silk_touch":1}}]
    execute at @s run data modify entity @s block_state.Name set from entity @n[type=item,nbt={Age:0s}] Item.id
    data modify entity @s block_state.Properties merge value {type:"double",waterlogged:"true"}
    execute at @s run kill @n[type=item,nbt={Age:0s}]
  }
  
  fn begin_animation() {
    data modify entity @s transformation.scale set value [0f,0f,0f]
    data modify entity @s start_interpolation set value 0
    setblock ~ ~ ~ air
    tp @s ~ ~20 ~
  }
}

fn reset() {
  execute as @e[type=villager,tag=harl.modern.villager,name=Dinnerbone,limit=1] at @s run function harl:modern/replace_villager
  execute at @e[type=marker,tag=harl.modern.bee_nest,limit=1] run setblock ~ ~ ~ bee_nest[facing=south,honey_level=5]
}

fn replace_villager() {
  kill @s
  summon villager ~ ~ ~ {NoAI:1b,Tags:["harl.modern.villager"],Rotation:[90f,0f]}
}
