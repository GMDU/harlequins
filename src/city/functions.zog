fn init() {
  execute in harl:void run function harl:city/init_in_dim
  schedule function harl:harlequins/init 4t
}

fn init_in_dim() {
  forceload add -1466 -1468 -1419 -1421
  function harl:city/place_template {integrity: 1f}
  forceload remove -1466 -1468 -1419 -1421
}

fn _tick() {
  execute in harl:void positioned -1442 10 -1424 as @a[distance=..3,tag=harl.modern.player,tag=!harl.city.player] run function harl:city/enter
}

fn enter() {
  tag @s remove harl.modern.player
  tag @s add harl.city.player

  loop()
}

fn exit() {
  execute in the_end run forceload add 0 0
  execute at @s run function harl:city/replace_inventory/init
  execute in the_end positioned 1 0 0 positioned over motion_blocking run tp @s ~ ~ ~
  execute in the_end run forceload remove 0 0
}

module replace_inventory {
  fn init() {
    summon armor_stand ~ ~ ~ {Marker:1b,Invisible:1b,Tags:["harl.inventory"]}
    iterate()
    kill @n[type=armor_stand,tag=harl.inventory]
  }

  fn iterate() {
    execute unless data storage harl:player inventory[] run return -1
    data modify entity @n[type=armor_stand,tag=harl.inventory] HandItems[0] set from storage harl:player inventory[0]
    function harl:city/replace_inventory/macro with storage harl:player inventory[0]
    data remove storage harl:player inventory[0]

    iterate()
  }

  fn macro() {
    /$item replace entity @s container.$(Slot) from entity @n[type=armor_stand,tag=harl.inventory] weapon.mainhand
  }
}

fn place_template() {
  gamerule doTileDrops false
  fill -1466 10 -1468 -1419 22 -1421 air
  /$place template harl:city/cityscape -1466 9 -1468 none none $(integrity) 5724715
  stopsound @a block
  gamerule doTileDrops true
}

fn remove_island() {
  execute in harl:void run fill -1419 9 -1468 -1466 21 -1421 air
}

fn loop() {
  execute if entity @a[tag=harl.city.player] run schedule function harl:city/loop 5t
  execute store result score $z harl.city run data get entity @a[tag=harl.city.player,limit=1] Pos[2]
  scoreboard players add $z harl.city 1425
  scoreboard players operation $z harl.city *= $-1 harl.city
  scoreboard players set $integrity harl.city 43
  scoreboard players operation $integrity harl.city -= $z harl.city
  scoreboard players operation $integrity harl.city *= $100 harl.city
  scoreboard players operation $integrity harl.city /= $43 harl.city
  execute if score $integrity harl.city = $old_z harl.city run return -1

  scoreboard players operation $old_z harl.city = $integrity harl.city
  execute store result storage harl:city loop.integrity float 0.01 run scoreboard players get $integrity harl.city
  execute in harl:void run function harl:city/place_template with storage harl:city loop
}